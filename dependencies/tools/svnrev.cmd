@echo off
IF NOT EXIST lastrev.txt (
    echo 0 > lastrev.txt
)

set REVISION=

for %%X in (svn.exe) do (set FOUNDSVN=%%~$PATH:X)


if defined FOUNDSVN (
	IF "%COMPUTERNAME%" == "ADRIAN-PC" (
		FOR /F "tokens=2 skip=5" %%G IN ('svn info --revision HEAD ..\..') DO IF NOT DEFINED REVISION SET REVISION=%%G
	) else (
		FOR /F "tokens=2 skip=4" %%G IN ('svn info --revision HEAD ..\..') DO IF NOT DEFINED REVISION SET REVISION=%%G
	)
) else (
    SET REVISION=99999

    echo BUILDNUMBER : warning BLD0001: svn.exe not located in the system PATH, defaulting revision to 99999
)


FOR /F %%H IN (lastrev.txt) DO SET LASTREVISION=%%H

echo Current revision is %REVISION%
echo Last revision is %LASTREVISION%

set /A REVISION_NEW = %REVISION% + 1200

IF %LASTREVISION% NEQ %REVISION% (
    echo // generated by svnrev.cmd > "..\..\VCS PC\buildnumber.h"
    echo #define BUILDNUMBER %REVISION_NEW% >> "..\..\VCS PC\buildnumber.h"
    echo #define BUILDNUMBER_STR "%REVISION_NEW%" >> "..\..\VCS PC\buildnumber.h"
    echo #define BUILDHOST "%COMPUTERNAME%" >> "..\..\VCS PC\buildnumber.h"
    echo %REVISION% > lastrev.txt
)