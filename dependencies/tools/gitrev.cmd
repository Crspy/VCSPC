@echo off
IF NOT EXIST lastrev.txt (
    echo 0 > lastrev.txt
)

set REVISION=

for %%X in (git.exe) do (set FOUNDSVN=%%~$PATH:X)


if defined FOUNDSVN (
	FOR /F "" %%G IN ('git rev-list HEAD --count') DO IF NOT DEFINED REVISION SET REVISION=%%G
) else (
    SET REVISION=99999

    echo BUILDNUMBER : warning BLD0001: git.exe not located in the system PATH, defaulting revision to 99999
)


FOR /F %%H IN (lastrev.txt) DO SET LASTREVISION=%%H

echo Current revision is %REVISION%
echo Last revision is %LASTREVISION%

set /A REVISION_NEW = %REVISION% + 1210

IF %LASTREVISION% NEQ %REVISION% (
    echo // generated by gitrev.cmd > "..\..\VCS PC\buildnumber.h"
    echo #define BUILDNUMBER %REVISION_NEW% >> "..\..\VCS PC\buildnumber.h"
    echo #define BUILDNUMBER_STR "%REVISION_NEW%" >> "..\..\VCS PC\buildnumber.h"
    echo #define BUILDHOST "%COMPUTERNAME%" >> "..\..\VCS PC\buildnumber.h"
    echo %REVISION% > lastrev.txt
)